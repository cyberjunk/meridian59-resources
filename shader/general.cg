struct VOut
{
	float4 p    : POSITION;
	float2 uv   : TEXCOORD0;
	float4 wp   : TEXCOORD1;
};

void ambient_vs(
	float4 p : POSITION,
	float2 uv : TEXCOORD0,
	uniform float4x4 wvpMat,
	uniform float4x4 texturemat,
	out float4 oPos : POSITION,
	out float2 oUV : TEXCOORD0)
{
	oPos = mul(wvpMat, p);
	oUV = mul(texturemat,float4(uv,0,1)).xy;
}

float4 ambient_ps(
	in float2 uv : TEXCOORD0,
	uniform float3 ambient,
	uniform float3 colormodifier,
	uniform float opaque,
	uniform sampler2D dMap): COLOR0
{
    return tex2D(dMap, uv) * float4(
		ambient[0] * colormodifier[0],
		ambient[1] * colormodifier[1],
		ambient[2] * colormodifier[2],		
		1.0 * opaque);
}

float4 ambient256_ps(
	in float2 uv : TEXCOORD0,
	uniform float3 ambient,
	uniform float3 colormodifier,
	uniform float opaque,
	uniform sampler2D palette : register(s0),
	uniform sampler2D texture : register(s1)): COLOR0
{
	if (uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0)
	{
		return float4(0,0,0,0);
	}
	else
	{
		return tex2D(palette, float2(tex2D(texture, uv).a, 0)) * float4(
			ambient[0] * colormodifier[0],
			ambient[1] * colormodifier[1],
			ambient[2] * colormodifier[2],		
			1.0 * opaque);
	}
}

VOut diffuse_vs(
	float4 p : POSITION,
	float2 uv : TEXCOORD0,
	uniform float4x4 wMat,
	uniform float4x4 wvpMat,
	uniform float4x4 texturemat)
{
	VOut OUT;
	
	OUT.wp = mul(wMat, p);
	OUT.p = mul(wvpMat, p);
	OUT.uv = mul(texturemat,float4(uv,0,1)).xy;
 
	return OUT;
}
 
float4 diffuse_ps(
	float2 uv : TEXCOORD0,
	float4 wp : TEXCOORD1,
	uniform float3 lightDif0,
	uniform float4 lightPos0,
	uniform float4 lightAtt0,
	uniform float3 colormodifier,
	uniform float opaque,
	uniform sampler2D diffuseMap : TEXUNIT0): COLOR0
{  
	// distance, attenuation
	half lightDist = length(lightPos0.xyz - wp.xyz) / lightAtt0.r;
	half la = 1.0 - (lightDist * lightDist);

	// the argb value from diffuse texture
	float4 diffuseTex = tex2D(diffuseMap, uv);
	
	// the outputcolor RGB, no alpha
	float3 diffuseContrib = (lightDif0 * diffuseTex.rgb * la);

	// return outputcolor RGB with alpha from texture
	return float4(
		diffuseContrib[0] * colormodifier[0],
		diffuseContrib[1] * colormodifier[1],
		diffuseContrib[2] * colormodifier[2],
		diffuseTex.a * opaque);
}

float4 diffuse256_ps(
	float2 uv : TEXCOORD0,
	float4 wp : TEXCOORD1,
	uniform float3 lightDif0,
	uniform float4 lightPos0,
	uniform float4 lightAtt0,
	uniform float3 colormodifier,
	uniform float opaque,
	uniform sampler2D palette : TEXUNIT0,
	uniform sampler2D texture : TEXUNIT1): COLOR0
{
	if (uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0)
	{
		return float4(0,0,0,0);
	}
	else
	{
		// distance, attenuation
		half lightDist = length(lightPos0.xyz - wp.xyz) / lightAtt0.r;
		half la = 1.0 - (lightDist * lightDist);

		// the argb value from diffuse texture
		float4 diffuseTex = tex2D(palette, float2(tex2D(texture, uv).a, 0));
		
		// the outputcolor RGB, no alpha
		float3 diffuseContrib = (lightDif0 * diffuseTex.rgb * la);

		// return outputcolor RGB with alpha from texture
		return float4(
			diffuseContrib[0] * colormodifier[0],
			diffuseContrib[1] * colormodifier[1],
			diffuseContrib[2] * colormodifier[2],
			diffuseTex.a * opaque);
	}
}

void invisible_vs(
		float4 pos			: POSITION,
		float4 normal		: NORMAL,
		float2 tex			: TEXCOORD0,
		
		out float4 oPos		: POSITION,
		out float3 noiseCoord : TEXCOORD0,
		out float4 projectionCoord : TEXCOORD1,
		out float3 oEyeDir : TEXCOORD2, 
		out float2 oUV : TEXCOORD3, 

		uniform float4x4 worldViewProjMatrix,
		uniform float4x4 texturemat,
		uniform float3 eyePosition, // object space
		uniform float timeVal,
		uniform float scale,  // the amount to scale the noise texture by
		uniform float scroll, // the amount by which to scroll the noise
		uniform float noise  // the noise perturb as a factor of the  time
		)
{
	oPos = mul(worldViewProjMatrix, pos);
	// Projective texture coordinates, adjust for mapping
	float4x4 scalemat = float4x4(0.5,   0,   0, 0.5, 
	                               0,-0.5,   0, 0.5,
								   0,   0, 0.5, 0.5,
								   0,   0,   0,   1);
	projectionCoord = mul(scalemat, oPos);
	// Noise map coords
	noiseCoord.xy = (tex + (timeVal * scroll)) * scale;
	noiseCoord.z = noise * timeVal;

	oEyeDir = normalize(pos.xyz - eyePosition); 
	
	oUV = mul(texturemat,float4(tex,0,1)).xy; 
	
}

void invisible_ps(
		float4 pos					: POSITION,
		float3 noiseCoord			: TEXCOORD0,
		float4 projectionCoord		: TEXCOORD1,
		float3 eyeDir				: TEXCOORD2,
		float2 uv					: TEXCOORD3,
		
		out float4 col		: COLOR,
		
		uniform float4 tintColour,
		uniform float noiseScale, 
		uniform sampler2D diffuseMap : register(s0),
		uniform sampler2D noiseMap : register(s1),
		uniform sampler2D refractMap : register(s2)
		)
{	
	// the argb value from diffuse texture
	float4 diffuseTex = tex2D(diffuseMap, uv);
	
	if (diffuseTex.a > 0.0)
	{
		// Do the tex projection manually so we can distort _after_
		float2 final = projectionCoord.xy / projectionCoord.w;

		// Noise
		float3 noiseNormal = (tex2D(noiseMap, (noiseCoord.xy / 5)).rgb - 0.5).rbg * noiseScale;
		final += noiseNormal.xz;

		// Final colour
		col = tex2D(refractMap, final) + tintColour;
	}	
	else
	{
		col = diffuseTex;
	}
}